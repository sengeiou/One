def isBuildApk = true
if (isBuildApk) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

//apply plugin: 'com.android.library'
apply plugin: 'com.ubt.alpha1e'
apply plugin: 'com.jakewharton.butterknife'

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools
    defaultConfig {
        if (isBuildApk){
            applicationId "com.ubt.loginmodule"
        }
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]
            }
        }

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            if (isBuildApk) {
                manifest.srcFile 'src/main/apkrun/AndroidManifest.xml'
            }
        }
    }

    if(!isBuildApk) {
        android.libraryVariants.all { variant ->
            variant.outputs.all {
                if (outputFileName.endsWith('.aar')) {
                    if (variant.buildType.name == "debug") {
                        outputFileName = "${archivesBaseName}-debug-${android.defaultConfig.versionName}.aar"
                    } else {
                        outputFileName = "${archivesBaseName}-${android.defaultConfig.versionName}.aar"
                    }
                }
            }
        }
    }

}
repositories {
    flatDir {
        dirs 'libs'
    }
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation deps.support.app_compat
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    //butterknife
    implementation deps.butterknife.runtime
    annotationProcessor deps.butterknife.compiler

    implementation(name: 'tvsloginlib', ext: 'aar')
    implementation ('com.tencent.mm.opensdk:wechat-sdk-android-with-mta:1.0.2'){
        exclude group: 'com.tencent.wup'
    }
}
